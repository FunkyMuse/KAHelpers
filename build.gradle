buildscript {

    ext {
        lifecycle = '2.4.0-alpha03'
        retrofit = "2.9.0"
        rxBindings = "4.0.0"
        glide = "4.12.0"
        constraint_layout = '2.1.0'
        coroutines = '1.5.2-native-mt'
        nav_version = '2.3.5'
        rx = '3.0.0'
        material = "1.4.0"
        moshi = "1.12.0"
        fragment = "1.3.6"
        activity = "1.3.1"
        appCompat = "1.3.1"
        coreKTX = "1.7.0-beta01"
        securityVersion = "1.1.0-alpha03"
        recyclerview = '1.2.1'
        annotationVersion = '1.2.0'
        documentFile = '1.0.1'
        swipeRefresh = '1.1.0'
        viewPager = '1.0.0'
        biometrics = '1.2.0-alpha03'
        joda = '2.10.9.1'
        exifinterface= '1.3.3'
        palette = '1.0.0'
        preference = '1.1.1'
        collection = '1.1.0'
        interpolator = '1.0.0'
        transition = '1.4.1'


        //app only
        room_version = "2.4.0-alpha04"


        //tests
        junitVersion = '4.13.2'
        hamcrestVersion = '1.3'
        androidXTestCoreVersion = '1.4.0'
        androidXTestExtKotlinRunnerVersion = '1.1.3'
        androidXTestRulesVersion = '1.2.0-beta01'
        robolectricVersion = '4.6.1'
        archTestingVersion = '2.1.0'
        espressoVersion = '3.4.0'

        //compilation
        compileVersion = 31
        minVersion = 21
        verCode = 1
        verName = "1.0.0"
        testRunner = "androidx.test.runner.AndroidJUnitRunner"

        kotlin_version = '1.5.30'
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}



allprojects {
    group = "io.github.funkymuse"

    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    switch (it.name) {
        case "app":
            apply plugin: 'com.android.application'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'kotlin-parcelize'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
            }
            applyAndroid(it, true)
            break

        case ["enums", "regex"]:
            apply plugin: 'java-library'
            apply plugin: 'kotlin'
            applyKotlinModule(it)

            break

        default:
            //setup gradle for libraries
            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'maven-publish'
            apply plugin: 'kotlin-parcelize'
            applyAndroid(it, false)

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                // Dependencies for local unit tests
                testImplementation "junit:junit:$junitVersion"
                testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
                testImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
                testImplementation "androidx.test:core-ktx:$androidXTestCoreVersion"
                testImplementation "org.robolectric:robolectric:$robolectricVersion"
                testImplementation "androidx.arch.core:core-testing:$archTestingVersion"
                testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"

                // AndroidX Test - Instrumented testing
                androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
                androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
            }


            it.afterEvaluate {
                publishing {
                    publications {
                        release(MavenPublication) {
                            from components.release
                        }
                    }
                }
            }


            break
    }

}

def applyKotlinModule(project) {
    project.java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    project.dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

def applyAndroid(project, buildConfigCase) {
    project.android {
        compileSdkVersion compileVersion

        defaultConfig {
            minSdkVersion minVersion
            targetSdkVersion compileVersion
            versionCode verCode
            versionName verName
            testInstrumentationRunner testRunner
        }

        compileOptions {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        testOptions.unitTests {
            includeAndroidResources = true
        }

        buildFeatures {
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            buildConfig = buildConfigCase
        }
    }

}
