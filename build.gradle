buildscript {

    ext {
        lifecycle = '2.6.2'
        retrofit = "2.9.0"
        okHttp = "4.11.0"
        rxBindings = "4.0.0"
        glide = "4.16.0"
        constraint_layout = '2.1.4'
        coroutines = '1.7.3'
        nav_version = '2.7.1'
        rx = '3.0.1'
        material = "1.9.0"
        moshi = "1.15.0"
        fragment = "1.6.1"
        activity = "1.7.2"
        appCompat = "1.6.1"
        coreKTX = "1.12.0"
        securityVersion = "1.1.0-alpha06"
        recyclerview = '1.3.1'
        annotationVersion = '1.6.0'
        documentFile = '1.0.1'
        swipeRefresh = '1.1.0'
        viewPager = '1.0.0'
        biometrics = '1.2.0-alpha05'
        joda = '2.12.5'
        exifinterface= '1.3.6'
        palette = '1.0.0'
        preference = '1.2.1'
        collection = '1.2.0'
        interpolator = '1.0.0'
        transition = '1.4.1'


        //app only
        room_version = "2.5.2"


        //tests
        junitVersion = '4.13.2'
        hamcrestVersion = '1.3'
        androidXTestCoreVersion = '1.5.0'
        androidXTestExtKotlinRunnerVersion = '1.1.5'
        androidXTestRulesVersion = '1.2.0-beta01'
        robolectricVersion = '4.10.3'
        archTestingVersion = '2.2.0'
        espressoVersion = '3.5.1'

        //compilation
        compileVersion = 33
        minVersion = 23
        verCode = 1
        verName = "1.0.0"
        testRunner = "androidx.test.runner.AndroidJUnitRunner"

        kotlin_version = '1.9.10'
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.dokka' version '1.9.0'
}

allprojects {
    group = "io.github.funkymuse"

    repositories {
        google()
        mavenCentral()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

subprojects {
    switch (it.name) {
        case "app":
            apply plugin: 'com.android.application'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'kotlin-parcelize'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
            }
            applyAndroid(it, true)
            break

        case ["enums", "regex"]:
            apply plugin: 'java-library'
            apply plugin: 'kotlin'
            apply plugin: 'org.jetbrains.dokka'
            applyKotlinModule(it)

            break

        default:
            //setup gradle for libraries
            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'maven-publish'
            apply plugin: 'kotlin-parcelize'
            apply plugin: 'org.jetbrains.dokka'

            applyAndroid(it, false)

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                // Dependencies for local unit tests
                testImplementation "junit:junit:$junitVersion"
                testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
                testImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
                testImplementation "androidx.test:core-ktx:$androidXTestCoreVersion"
                testImplementation "org.robolectric:robolectric:$robolectricVersion"
                testImplementation "androidx.arch.core:core-testing:$archTestingVersion"
                testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"

                // AndroidX Test - Instrumented testing
                androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
                androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
            }


            it.afterEvaluate {
                publishing {
                    publications {
                        release(MavenPublication) {
                            from components.release
                        }
                    }
                }
            }


            break
    }

}

def applyKotlinModule(project) {
    project.java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    project.dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

def applyAndroid(project, buildConfigCase) {
    project.android {
        compileSdkVersion compileVersion

        defaultConfig {
            minSdkVersion minVersion
            targetSdkVersion compileVersion
            versionCode verCode
            versionName verName
            testInstrumentationRunner testRunner
        }

        compileOptions {
            sourceCompatibility = 17
            targetCompatibility = 17
        }

        kotlinOptions {
            jvmTarget = "17"
        }

        testOptions.unitTests {
            includeAndroidResources = true
        }

        buildFeatures {
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            buildConfig = buildConfigCase
        }
    }

}
